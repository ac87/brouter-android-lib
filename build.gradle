// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        kotlin_version = '1.4.10'
        detekt_version = '1.12.0'
        klint_plugin_version = '9.4.0'
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$klint_plugin_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "$detekt_version"
    id "com.diffplug.gradle.spotless" version "3.27.2"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            name = "BRouter GitHubPackages"
            url = uri("https://maven.pkg.github.com/abrensch/brouter")
            credentials {
                username = project.properties['GITHUB_USERNAME'] ?: ""
                // need a package read token from https://github.com/settings/tokens
                password = project.properties['GITHUB_TOKEN'] ?: ""
            }
        }
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = 'mapbox'
                // need a download token https://account.mapbox.com
                password = project.properties['MAPBOX_DOWNLOADS_TOKEN'] ?: ""
            }
        }
    }
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        format 'misc', {
            target '**/*.gradle'
            trimTrailingWhitespace()
            indentWithSpaces(4)
            endWithNewline()
        }
        kotlin {
            target "**/*.kt"
            ktlint('0.33.0')
        }
    }
}
